// For Shithri's drink quest - spawn the seaman

//
// When you have at least 9 liquors, the seaman appears
// in the Sea Bounty tavern.
//

// First, set "ready". If the party is in the Sea Bounty tavern,
// they need to leave it before the seaman appears.
IF
  GlobalGT("6W#ShithriDrinksHasUniqueDrinks","GLOBAL",8)
  Global("6W#ShithriDrinksSeamanSpawn","GLOBAL",2)
THEN
  RESPONSE #100
    CreateCreature("6WPIRDR",[443.540],NW)
    SetGlobal("6W#ShithriDrinksSeamanSpawn","GLOBAL",3)
END

// Implementation Note:
// The Thumb has no script of his own, so he'll use area script (for simplicity).
//
// Note that it's not really possible to scatter NPCs nicely
// in the general scenario, as there is no option to iterate over all
// NPCs in a location at runtime.
//
// If any other mods introduce new NPCs to Sea's Bounty and those NPCs
// should be watching the duel - compatibility should be added explicitly.
//
// It's also assumed that the original locations, facing direction
// and - most importantly - scripts for the original NPCs are
// as in the original game (although the scripts may have been extended).
IF
  Global("6W#ShithriDrinksStarting","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("6W#ShithriDrinksStarting","GLOBAL",2)
    ClearAllActions()
    StartCutSceneMode()
    FadeToColor([20.0],0)
    Wait(2)

    // NPCs should cease to move for the time being
    //Note: they should get their original scripts back later on
    ActionOverride("SBCUST01", ChangeAIScript("",DEFAULT))
    ActionOverride("SBCUST02", ChangeAIScript("",DEFAULT))
    ActionOverride("SBWENCH",  ChangeAIScript("",DEFAULT))
    ActionOverride("SBSLUM01", ChangeAIScript("",DEFAULT))
    SmallWait(1)

    StorePartyLocations()
    ActionOverride("6WPIRDIR", JumpToPoint([443.540]))
    ActionOverride(Player1,    JumpToPoint([482.500]))
    ActionOverride(Player2,    JumpToPoint([518.437]))
    ActionOverride(Player3,    JumpToPoint([486.414]))
    ActionOverride(Player4,    JumpToPoint([457.404]))
    ActionOverride(Player5,    JumpToPoint([439.379]))
    ActionOverride(Player6,    JumpToPoint([411.367]))
    ActionOverride("SBCUST01", JumpToPoint([379.464]))
    ActionOverride("SBCUST02", JumpToPoint([384.570]))
    ActionOverride("SBWENCH",  JumpToPoint([334.518]))
    ActionOverride("SBSLUM01", JumpToPoint([271.562]))
    ActionOverride("SBSLUM02", JumpToPoint([289.551]))

    ActionOverride("6WPIRDIR", Face(NW))
    ActionOverride(Player1,    Face(NW))
    ActionOverride(Player2,    Face(SW))
    ActionOverride(Player3,    Face(SW))
    ActionOverride(Player4,    Face(SW))
    ActionOverride(Player5,    Face(SW))
    ActionOverride(Player6,    Face(SW))
    ActionOverride("SBCUST01", Face(SE))
    ActionOverride("SBCUST02", Face(NW))
    ActionOverride("SBWENCH",  Face(SE))
    ActionOverride("SBSLUM01", Face(SE))
    ActionOverride("SBSLUM02", Face(SE))

    // the Thumb switches off his soundset
    // so that it doesn't play during his
    // judge comments
    SetPlayerSound("THUMB", -1, SELECT_COMMON1)
    SetPlayerSound("THUMB", -1, SELECT_COMMON2)
    SetPlayerSound("THUMB", -1, SELECT_COMMON3)
    SetPlayerSound("THUMB", -1, SELECT_COMMON4)

    FadeFromColor([20.0],0)
    Wait(1)
    SmallWait(1)
    EndCutSceneMode()
    ActionOverride("THUMB", StartDialogNoSet(Player1))
END

IF
  Global("6W#ShithriDrinksEnding","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("6W#ShithriDrinksEnding","GLOBAL",2)
    SetGlobal("6W#ShithriDrinksActive","GLOBAL",5) // quest done
    ClearAllActions()
    StartCutSceneMode()
    FadeToColor([20.0],0)
    Wait(2)

    //TODO: move other characters around so it's more realistic?
    RestorePartyLocations()
    ActionOverride("SBCUST01", JumpToPoint([689.251]))
    ActionOverride("SBCUST02", JumpToPoint([485.278]))
    ActionOverride("SBWENCH",  JumpToPoint([664.350]))
    ActionOverride("SBSLUM01", JumpToPoint([601.240]))
    ActionOverride("SBSLUM02", JumpToPoint([643.236]))
    SmallWait(1)

    ActionOverride("SBCUST01", Face(SE))
    ActionOverride("SBCUST02", Face(NW))
    ActionOverride("SBWENCH",  Face(E))
    ActionOverride("SBSLUM01", Face(E))
    ActionOverride("SBSLUM02", Face(W))
    SmallWait(1)

    // NPCs should cease to move for the time being
    //Note: they should get their original scripts back later on
    ActionOverride("SBCUST01", ChangeAIScript("WDRUNSGT",DEFAULT))
    ActionOverride("SBCUST02", ChangeAIScript("",DEFAULT))
    ActionOverride("SBWENCH",  ChangeAIScript("WDRUNSGT",DEFAULT))
    ActionOverride("SBSLUM01", ChangeAIScript("WTRUNSGT",DEFAULT))
    ActionOverride("SBSLUM02", ChangeAIScript("WDRUNSGT",DEFAULT))

    // the Thumb is an inn-keeper again
    SetPlayerSound("THUMB", 60246, SELECT_COMMON1)
    SetPlayerSound("THUMB", 60247, SELECT_COMMON2)
    SetPlayerSound("THUMB", 60248, SELECT_COMMON3)
    SetPlayerSound("THUMB", 60249, SELECT_COMMON4)

    // Franky goes away
    ActionOverride("6WPIRDR", DestroySelf())

    FadeFromColor([20.0],0)
    SmallWait(1)
    EndCutSceneMode()
END

// Shithri may comment on the duel's result if she's in the party
IF
  Global("6W#ShithriDrinksEnding","GLOBAL",2)
  IfValidForPartyDialog("6WSHITHRI")
THEN
  RESPONSE #100
    SetGlobal("6W#ShithriDrinksEnding","GLOBAL",3)
    ActionOverride("6WSHITHRI", StartDialog(Player1))
END
IF
  Global("6W#ShithriDrinksEnding","GLOBAL",2)
THEN
  RESPONSE #100
    SetGlobal("6W#ShithriDrinksEnding","GLOBAL",3)
END

// PC to guess Franky's drink
IF
  GlobalGT("6W#ShithriDrinksPcRound","GLOBAL",0)
  // If the drink is being checked for being evil, a different action should take place.
  Global("6W#ShithriDrinksThisEvil","GLOBAL",0)
THEN
  RESPONSE #100
    ActionOverride(Player1,StartDialog("6WDRINK",Player1))
END

// PC to check for evilness of the drink.
// Picks the innate, cleric or wizard Detect Evil.
// Uses a variable to avoid the problem of filtering prepared spells
// of a creature other than the active one.
IF
  GlobalGT("6W#ShithriDrinksPcRound","GLOBAL",0)
  Global("6W#ShithriDrinksThisEvil","GLOBAL",1)
  Global("6W#ShithriDrinksThisEvilSpell","GLOBAL",1)
THEN
  RESPONSE #100
    ClearAllActions()
    StartCutSceneMode()
    ActionOverride(Player1,SpellRES("SPCL212",Myself))
    Wait(4)
    SetGlobal("6W#ShithriDrinksThisEvil","GLOBAL",2)
    EndCutSceneMode()

    ActionOverride(Player1,StartDialog("6WDRINK",Player1))
END
IF
  GlobalGT("6W#ShithriDrinksPcRound","GLOBAL",0)
  Global("6W#ShithriDrinksThisEvil","GLOBAL",1)
  Global("6W#ShithriDrinksThisEvilSpell","GLOBAL",2)
THEN
  RESPONSE #100
    ClearAllActions()
    StartCutSceneMode()
    ActionOverride(Player1,SpellRES("SPPR104",Myself))
    Wait(4)
    SetGlobal("6W#ShithriDrinksThisEvil","GLOBAL",2)
    EndCutSceneMode()

    ActionOverride(Player1,StartDialog("6WDRINK",Player1))
END
IF
  GlobalGT("6W#ShithriDrinksPcRound","GLOBAL",0)
  Global("6W#ShithriDrinksThisEvil","GLOBAL",1)
  Global("6W#ShithriDrinksThisEvilSpell","GLOBAL",3)
THEN
  RESPONSE #100
    ClearAllActions()
    StartCutSceneMode()
    ActionOverride(Player1,SpellRES("SPWI202",Myself))
    Wait(4)
    SetGlobal("6W#ShithriDrinksThisEvil","GLOBAL",2)
    EndCutSceneMode()

    ActionOverride(Player1,StartDialog("6WDRINK",Player1))
END
IF
  GlobalGT("6W#ShithriDrinksPcRound","GLOBAL",0)
  Global("6W#ShithriDrinksThisEvil","GLOBAL",1)
  Global("6W#ShithriDrinksThisEvilSpell","GLOBAL",4)
THEN
  RESPONSE #100
    ClearAllActions()
    StartCutSceneMode()
    ActionOverride(Player1,SpellRES("SPPR101",Myself))
    Wait(7)
    PlaySound("EFF_M89")
    Wait(3)
    SetGlobal("6W#ShithriDrinksThisEvil","GLOBAL",3)
    EndCutSceneMode()

    ActionOverride(Player1,StartDialog("6WDRINK",Player1))
END
IF
  GlobalGT("6W#ShithriDrinksPcRound","GLOBAL",0)
  Global("6W#ShithriDrinksThisEvil","GLOBAL",1)
  Global("6W#ShithriDrinksThisEvilSpell","GLOBAL",5)
THEN
  RESPONSE #100
    ClearAllActions()
    StartCutSceneMode()
    ActionOverride(Player1,SpellRES("SPPR203",Myself))
    Wait(7)
    PlaySound("EFF_M89")
    Wait(3)
    SetGlobal("6W#ShithriDrinksThisEvil","GLOBAL",3)
    EndCutSceneMode()

    ActionOverride(Player1,StartDialog("6WDRINK",Player1))
END
